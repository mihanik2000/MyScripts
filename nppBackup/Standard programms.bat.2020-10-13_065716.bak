@echo off

Rem Скрипт установки стандартного пакета программ с моего репозитория

rem Windows XP не поддерживается!!!
ver | find "5.1."

If %errorlevel%==0  (
	Echo Windows XP unsupported!!!
	Exit /b 1
 ) 

rem ****************************************************************************************
rem Проверяем наличие у пользователя админских прав...
rem ****************************************************************************************

SET HasAdminRights=0
FOR /F %%i IN ('WHOAMI /PRIV /NH') DO (
	IF "%%i"=="SeTakeOwnershipPrivilege" SET HasAdminRights=1
)

IF NOT %HasAdminRights%==1 (
	ECHO .
	ECHO You need administrator rights to run!!!
	ECHO .
	GOTO END
)

rem ****************************************************************************************
rem Описываем переменные.
rem ****************************************************************************************
set MyFolder=%SystemRoot%\TMP\Mihanikus

set URL7Zip="https://www.7-zip.org/a/7z1900.msi"
set URL7Zip-x64="https://www.7-zip.org/a/7z1900-x64.msi"


rem ****************************************************************************************
rem Настраиваем учётные записи пользователей на компьютере
rem ****************************************************************************************

rem активируем встроенного Админа
net user Администратор "AdminPass" /active:yes

rem Создадим пользователя user с паролем 321
net user user 321 /add

rem ****************************************************************************************
rem Настроим некоторые параметры необходимые для удалённого администрирования.
rem ****************************************************************************************

rem включим режим электропитания "Высокая производительность"
powercfg /setactive 8c5e7fda-e8bf-4a96-9a85-a6e23a8c635c

rem отключаем спящий режим
powercfg -hibernate off
reg ADD HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Control\Power /v HiberFileSizePercent /t REG_DWORD /d 0 /f
reg ADD HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Control\Power /v HibernateEnabled /t REG_DWORD /d 0 /f

rem Запланируем ежедневное выключение в полночь
SCHTASKS /Create /RU "NT AUTHORITY\SYSTEM" /SC DAILY /TN "Microsoft\Office\Office Shutdown" /TR  "\"C:\Windows\System32\shutdown.exe\" /s /f /t 00"  /ST 00:00 /RL HIGHEST

rem Включаем ADMIN шару
reg add HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Policies\System /v LocalAccountTokenFilterPolicy /t reg_sz /d 1 /f

rem Включим межсетевой экран
netsh advfirewall set allprofiles state on

rem Разрешим отвечать на ping 
netsh firewall set icmpsetting 8

rem ****************************************************************************************
rem Настроим возможность подключаться по RDP
rem ****************************************************************************************

rem Включим возможность использования RDP
reg add "HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Control\Terminal Server" /v fDenyTSConnections /t REG_DWORD /d 0 /f
     
rem Включим возможность использования и удалённого помощника
reg add "HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Control\Terminal Server" /v fAllowToGetHelp /t REG_DWORD /d 1 /f

rem Исключение сетевого экрана для RDP
netsh advfirewall firewall del rule name="AlowRDP"
netsh firewall del portopening tcp 3389

netsh firewall set service remoteadmin enable
netsh firewall set service remotedesktop enable

netsh advfirewall firewall add rule name="AlowRDP" protocol="TCP" localport=3389 action=allow dir=IN
netsh firewall set portopening tcp 3389 AlowRDP enable 

Rem укажем режим запуска службы RDP  в auto
sc config TermService start= auto

rem Запустим службу RDP
net start TermService

rem Добавляем утилиту certutil.exe в исключения брандмауера Windows
netsh advfirewall firewall del rule name="Certutil"
netsh firewall add allowedprogram "C:\Windows\System32\certutil.exe" Certutil
netsh advfirewall firewall add rule name="Certutil" dir=in action=allow program="C:\Windows\System32\certutil.exe"

rem ****************************************************************************************
rem Начинаем устанавливать все программы по очереди
rem ****************************************************************************************

rem Переходим на системный диск
%SystemDrive%

rem Создаём папку для хранения дистрибутивов и переходим в неё
mkdir "%MyFolder%"
cd "%MyFolder%"

ECHO Install curl...
ECHO .
mkdir  "%ProgramFiles%\curl\"

If exist "%SystemDrive%\Program Files (x86)" (
	certutil -urlcache -split -f "http://repo.mihanik.net/curl/win64/libcurl-x64.dll" "%ProgramFiles%\curl\libcurl-x64.dll"
	certutil -urlcache -split -f "http://repo.mihanik.net/curl/win64/curl.exe" "%ProgramFiles%\curl\curl.exe"
	certutil -urlcache -split -f "http://repo.mihanik.net/curl/win64/curl-ca-bundle.crt" "%ProgramFiles%\curl\curl-ca-bundle.crt"
	certutil -urlcache -split -f "http://repo.mihanik.net/curl/win64/libcurl-x64.def" "%ProgramFiles%\curl\libcurl-x64.def"

 ) else (
	certutil -urlcache -split -f "http://repo.mihanik.net/curl/win32/libcurl.dll" "%ProgramFiles%\curl\libcurl.dll"
	certutil -urlcache -split -f "http://repo.mihanik.net/curl/win32/curl.exe" "%ProgramFiles%\curl\curl.exe"
	certutil -urlcache -split -f "http://repo.mihanik.net/curl/win32/curl-ca-bundle.crt" "%ProgramFiles%\curl\curl-ca-bundle.crt"
	certutil -urlcache -split -f "http://repo.mihanik.net/curl/win32/libcurl.def" "%ProgramFiles%\curl\libcurl.def"
)

rem Установим скачанный сертификат для всех пользователей
certutil -f -addstore "My" "%ProgramFiles%\curl\curl-ca-bundle.crt"

rem Добавим путь к curl в path
setx PATH "%ProgramFiles%\curl\;%Path%"
PATH=%ProgramFiles%\curl\;%Path%

rem Добавляем утилиту curl.exe в исключения брандмауера Windows
netsh advfirewall firewall del rule name="curl"
netsh firewall add allowedprogram "%ProgramFiles%\curl\curl.exe" Certutil
netsh advfirewall firewall add rule name="curl" dir=in action=allow program="%ProgramFiles%\curl\curl.exe"

ECHO .
ECHO Install 7-Zip...
ECHO .
 If exist "%SystemDrive%\Program Files (x86)" (
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\7z-x64.msi" %URL7Zip-x64%
		start /wait 7z-x64.msi /passive
	) else (
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\7z.msi" %URL7Zip%
		start /wait 7z.msi /passive
	)

ECHO Install dotNetFx3.5...
	Dism /online /enable-feature /featurename:NetFx3
	
ECHO Install dotNetFx4.8...
	"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\ndp48.exe" "http://repo.mihanik.net/Microsoft/Microsoft_NET/ndp48-x86-x64-allos-enu.exe"
	Start /wait ndp48.exe /q /norestart
	
ECHO Install Duplicati...
	If exist "%SystemDrive%\Program Files (x86)" (
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\duplicati-x64.msi" "https://updates.duplicati.com/beta/duplicati-2.0.5.1_beta_2020-01-18-x64.msi"
		Start /wait duplicati-x64.msi /passive
	 ) else (
 		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\duplicati-x86.msi" "https://updates.duplicati.com/beta/duplicati-2.0.5.1_beta_2020-01-18-x86.msi"
		Start /wait duplicati-x86.msi  /passive
 	)

rem Исключение сетевого экрана для Duplicati
netsh advfirewall firewall del rule name="Duplicati"
netsh firewall del portopening tcp 8200
netsh advfirewall firewall add rule name="Duplicati" protocol="TCP" localport=8200 action=allow dir=IN
netsh firewall set portopening tcp 8200 Duplicati enable 

"%ProgramFiles%\Duplicati 2\Duplicati.WindowsService.exe" install

sc start Duplicati

del /q /s "C:\ProgramData\Microsoft\Windows\Start Menu\Programs\Startup\Duplicati 2.lnk"

ECHO Install Java SE Runtime Environment...
	If exist "%SystemDrive%\Program Files (x86)" (
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\jre-8u201-windows-x64.exe" "http://repo.mihanik.net/Java/jre-8u241-windows-x64.exe"
		Start /wait jre-8u201-windows-x64.exe  /s
	) else (
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\jre-8u201-windows-i586.exe" "http://repo.mihanik.net/Java/jre-8u241-windows-i586.exe"
		Start /wait jre-8u201-windows-i586.exe  /s
	)

ECHO Install LibreOffice...
	If exist "%SystemDrive%\Program Files (x86)" (
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\LibreOffice_x64.msi" "http://libreoffice-mirror.rbc.ru/pub/libreoffice/libreoffice/stable/7.0.1/win/x86_64/LibreOffice_7.0.1_Win_x64.msi"
		Start /wait LibreOffice_x64.msi  /passive
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\LibreOffice_x64_helppack_ru.msi" "http://libreoffice-mirror.rbc.ru/pub/libreoffice/libreoffice/stable/7.0.1/win/x86_64/LibreOffice_7.0.1_Win_x64_helppack_ru.msi"
		Start /wait LibreOffice_x64_helppack_ru.msi  /passive

	) else (
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\LibreOffice_x86.msi" "http://libreoffice-mirror.rbc.ru/pub/libreoffice/libreoffice/stable/7.0.1/win/x86/LibreOffice_7.0.1_Win_x86.msi"
		Start /wait LibreOffice_x86.msi  /passive
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\LibreOffice_x86_helppack_ru.msi" "http://libreoffice-mirror.rbc.ru/pub/libreoffice/libreoffice/stable/7.0.1/win/x86/LibreOffice_7.0.1_Win_x86_helppack_ru.msi"
		Start /wait LibreOffice_x86_helppack_ru.msi  /passive
	)

ECHO Install Unreal Commander...
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\uncomsetup.exe" "https://unrealcommander.org/download/evolution/uncomsetup3.57(build1486).exe"
		start uncomsetup.exe /SILENT

ECHO Install Google Chrome...
	"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\ChromeSetup.exe" "http://repo.mihanik.net/ChromeSetup.exe"
	start /wait ChromeSetup.exe /silent /install

ECHO Install Skype
	"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\Skype.exe" "http://repo.mihanik.net/Skype.exe"
	start /wait Skype.exe  /silent

ECHO Install Notepad++...
	If exist "%SystemDrive%\Program Files (x86)" (
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\npp.7.8.4.Installer.x64.exe" "http://repo.mihanik.net/Notepad/npp.7.8.4.Installer.x64.exe"
		 Start /wait npp.7.8.4.Installer.x64.exe  /S
	 ) else (
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\npp.7.8.4.Installer.exe" "http://repo.mihanik.net/Notepad/npp.7.8.4.Installer.exe"
 		Start /wait npp.7.8.4.Installer.exe  /S
 	)

ECHO Install Thunderbird...
	If exist "%SystemDrive%\Program Files (x86)" (
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\ThunderbirdSetup68.5.0-x64.exe" "http://repo.mihanik.net/MozillaTB/ThunderbirdSetup68.5.0-x64.exe"
		 Start /wait ThunderbirdSetup68.5.0-x64.exe -ms
	 ) else (
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\ThunderbirdSetup68.5.0.exe" "http://repo.mihanik.net/MozillaTB/ThunderbirdSetup68.5.0.exe"
 		Start /wait ThunderbirdSetup68.5.0.exe -ms
 	)

ECHO Install FirefoxSetup...
	If exist "%SystemDrive%\Program Files (x86)" (
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\FirefoxSetup.x64.exe" "http://repo.mihanik.net/Firefox/FirefoxSetup.x64.exe"
		 Start /wait FirefoxSetup.x64.exe /SILENT
	 ) else (
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\FirefoxSetup.exe" "http://repo.mihanik.net/Firefox/FirefoxSetup.exe"
 		Start /wait FirefoxSetup.exe /SILENT
 	)

ECHO Install LiteManager Pro...
	"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\LiteManagerPro-Server.msi" "http://repo.mihanik.net/LiteManager/LiteManagerPro-Server.msi"
 	Start /wait LiteManagerPro-Server.msi /passive	
	sc start ROMService

ECHO Install Adobe Acrobat Reader...
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\AcroRdrDC1900820071_ru_RU_win.exe" "http://repo.mihanik.net/Adobe_Acrobat_Reader/AcroRdrDC1900820071_ru_RU.exe"
		Start /wait AcroRdrDC1900820071_ru_RU_win.exe /sPB

REM ECHO ...
REM ECHO Install Adobe flash player
REM ECHO ...

rem Windows 7?
rem ver | find "6.1."

rem If %errorlevel%==0  (
rem 		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\WinXP-7-Chrome-install_flash_player_ppapi.exe" "http://repo.mihanik.net/Adobe_Flash_Player/WinXP-7-Chrome-install_flash_player_ppapi.exe"
rem 		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\WinXP-7-Explorer-install_flash_player_ax.exe" "http://repo.mihanik.net/Adobe_Flash_Player/WinXP-7-Explorer-install_flash_player_ax.exe"
rem 		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\WinXP-7-Firefox-install_flash_player.exe" "http://repo.mihanik.net/Adobe_Flash_Player/WinXP-7-Firefox-install_flash_player.exe"
rem 		Start /wait WinXP-7-Chrome-install_flash_player_ppapi.exe /VERYSILENT
rem 		Start /wait WinXP-7-Explorer-install_flash_player_ax.exe /VERYSILENT
rem 		Start /wait WinXP-7-Firefox-install_flash_player.exe /VERYSILENT
rem  ) else (
rem 		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\Win8-10-Chrome-install_flash_player_ppapi.exe" "http://repo.mihanik.net/Adobe_Flash_Player/Win8-10-Chrome-install_flash_player_ppapi.exe"
rem 		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\Win8-10-Firefox-install_flash_player.exe" "http://repo.mihanik.net/Adobe_Flash_Player/Win8-10-Firefox-install_flash_player.exe"
rem 		Start /wait Win8-10-Chrome-install_flash_player_ppapi.exe /VERYSILENT
rem 		Start /wait Win8-10-Firefox-install_flash_player.exe /VERYSILENT
rem )

ECHO Install K-Lite_Codec_Pack...
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\K-Lite_Codec_Pack_1425_Mega.exe" "http://repo.mihanik.net/K-Lite/K-Lite_Codec_Pack_1425_Mega.exe"
		Start /wait K-Lite_Codec_Pack_1425_Mega.exe /silent

ECHO Install AIMP...
		"%ProgramFiles%\curl\curl.exe" -k -o "%MyFolder%\aimp_4.60.2177.exe" "http://repo.mihanik.net/Aimp/aimp_4.60.2177.exe"
		Start /wait aimp_4.60.2177.exe /AUTO /SILENT

:CONTINUE
	ECHO .
	ECHO Всё!
	ECHO .
	
:END

PAUSE

EXIT /B
